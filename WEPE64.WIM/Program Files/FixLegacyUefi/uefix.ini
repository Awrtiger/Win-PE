//Edit by caocaofff
CALL @UEFIX

_SUB UEFIX,W283H160,UEFI引导修复,,SHELL32.DLL#46
FDRV AllDrive=
ENVI WinDrv=
GROU Group1,L8T8W184H120,收集信息
LABE 标签1,L16T28W133H15,ESP分区:
LIST ESP,L65T24W50H600,%AllDrive%,,
LABE L1,L16T53W133H15,（没找到ESP分区？尝试
ITEM 按钮1,L145T49W30H20,挂载,CALL MountESP,,0
LABE L2,L177T53W7H15,）
LABE 标签2,L16T80W138H16,Windows系统根目录:
ITEM WIN,L126T75W30H20,...,CALL SelectWinDrv,,0
LABE 标签3,L16T106W138H16,系统语言:
LIST LAN,L72T102W60H600,cs-CZ|da-DK|de-DE|el-GR|en-US|es-ES|fi-FI|fr-FR|hu-HU|it-IT|ja-JP|ko-KR|nb-NO|nl-NL|pl-PL|pt-BR|pt-PT|ru-RU|sv-SE|tr-TR|zh-CN|zh-HK|zh-TW,,zh-CN,0x4
ITEM 按钮2,L200T14W69H24,重启计算机,CALL SHUTR,,0
ITEM 按钮3,L200T72W69H24,命令提示符,EXEC CMD.EXE /k @ECHO 提示： 控制启动管理器:bcdedit  主启动代码更新:bootsect  启动文件修复：bcdboot  
ITEM 按钮4,L200T102W69H24,开始修复,CALL FIX,,0
_END

_SUB MountESP
FIND $%PROCESSOR_ARCHITECTURE%=x64,EXEC =*MountESP64.EXE!EXEC =*MountESP.EXE
MESS+icon102 挂载成功！
KILL \UEFI引导修复
CALL @UEFIX
_END

_SUB SelectWinDrv
BROW Tag,*C:\Windows,请选择 Windows目录：
FIND $%Tag%=,!ENVI WinDrv=%Tag%	
_END

_SUB FIX
FIND $%ESP%=,TEAM MESS+icon0 请选择 ESP分区@ 错误#OK|EXIT
IFEX %WinDrv%,!TEAM MESS+icon0 请选择 Windows系统根目录@ 错误#OK|EXIT
MESS     ESP分区             为 %ESP%\n\n    Windows系统根目录   为 %WinDrv%\n\n    系统语言            为 %LAN%\n\n\n点击“是”开始修复，点击“否”返回修改     @确认信息#YN
FIND $%YESNO%=NO,EXIT
EXEC =*CMD.EXE /k bcdboot.exe %WinDrv%\ /l %LAN% /s %ESP% /f uefi
MESS+icon102 修复成功！
_END

_SUB SHUTR
MESS 你确定马上重启计算机吗？@信息#YN
FIND $%YESNO%=NO,EXIT
SHUT R
_END